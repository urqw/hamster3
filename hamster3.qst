:1
pln Похождения отважного хомяка Семена-3: На Полу
btn play, Начать игру
btn auto, Автор(ы)
btn intinctt, Инструкции
end

:auto
pln Идея____________Triangle
pln Программист_____DroZ
pln Сценарий________DroZ
pln Наборщик________DroZ
pln В главной роли__Ныне покойный хомяк Семен 
pln 
pln Пишите человеку который придумал Семена на triangle@zmail.ru
pln Пишите мне на ghost2002@xakep.ru
btn 1, Назад
end

:intinctt
pln Тут инструкции не нужны! И так все понятно! 
btn 1, Назад
end

:play
pln Вы - хомяк по кличке Семен. Если Вы прошли игру "Похождения отважного хомяка Семена: ПОБЕГ", то должны помнить, что Вас коварно заточили в клетку, Вас сторожил кот, а Вы, шуганув кота и взломав клетку гвоздем, все-таки вышли на свободу. Если Вы прошли игру "Похождения отважного хомяка Семена-2: КРУТОЙ СПУСК", то должны помнить что потом вы спустились со стола на веревку которую перед этим прилепили к столу на жвачку и прикрыли ее от солнца с помощью тени отбрасываемой шалашиком из спичек.
pln И вот теперь, Вы, в роли хомяка Семена должны пробраться к выходу из дома...
btn smotr, Осмотреться
end

:smotr
pln Ты осматриваешься: здесь стоит большой (Для тебя) стул, еще больше
pln стол (Тоже для тебя) с которого свисает веревка прилепленная на
pln жвачке, в углу высится горсть шелухи от семечек. Вот и все.
pln Хотя нет.... Около двери на улицу ты замечаешь того самого 
pln кота которого недавно облил молоком из поилки стоящей в твоем 
pln домике.
btn street2222, Подойти к двери на улицу
btn semiana, Покопаться в шелухе
btn verevka, Залезть по веревке на стол.
btn stuul, Подойти к стулу.
end

:verevka
pln Ты долго пыхтя и надуваясь лез по веревке - все таки 
здт спускаться со стола легче чем подниматься !
pln Наконец ты залез на стол с которого совсем недавно спустился.
goto stolik2002
end

:stolik2002
p Ты на столе. 
if steklo=0 then pln Здесь стоит большая красивая ваза.
if salasik=0 then pln Стоит небольшой шалашик из спичек слепленный жвачкой (Чтоб не упал).
p Здесь стоит твоя клетка (БЫВШАЯ !!!)
if steklo=1 then p Еще есть шалашик из спичек.
if salasik=1 then p Есть красивая ваза.
btn pds02, Заглянуть под стол 
btn kletochka, Зайти в клетку
if steklo=0 then btn vaza02, Подойти к вазе
if steklo=1 then btn vaza02, Подойти к месту где была ваза
btn spusk, Спуститься со стола
end

:kletochka
pln Ты стоишь в пустой клетке. Слева от тебя лежит кормушка, сделанная из крышки кофе Нескафе. Правда, пустая. Справа поилка, а впереди шикарный тренажер (Беговое колесо, о котором мечтает каждый заточенный в клетку хомяк).  Повсюду валяются бумажки (Чтобы спать было мягче).
btn kormushka, Подойти к кормушке
btn poilka, Подойти к поилке
btn sleep, Поспать
btn kolesa, Побегать в колесе
btn stolik2002, На стол
end

:sleep
pln Ты засыпаешь спокойным сном. Но тебе не суждено проснуться.
pln Хозяин дома, вернувшись домой обнаруживает следы твоей
pln умственной деятельности и понимает что тебя надо усыпить.
invkill
perkill
btn play, Сначала
end

:kolesa
pln Ты начинаешь бежать в колесе. Оно жутко скрипит, но крутиться. Кот не прибегает. "Еще бы" - ухмыляешься ты - "не хочет опять кислое молочко пить !"
btn kletochka, Назад
end

:milkp
pln Ты выпиваешь молоко. На вкус оно оказывается вполне нормальное.
moloko=1
btn kletochka, Назад
end

:milkv
pln Ты заливаешь остатки молока себе за щеку. Она красиво раздувается.
inv+ Остатки молока
moloko=1
bulbulbul=1
btn kletochka, Назад
end

:poilka
if moloko=0 then pln Ты подходишь к поилке и обнаруживаешь что молоко использовано (На кота) не все. & btn milkp, Вылакать остатки молока & btn milkv, Набрать остатки за щеку
if moloko=1 then pln Поилка теперь пустая.
btn kletochka, Назад
end

:kormushka
pln Ты подходишь к кормушке, оглядываешь ее в поисках чего нибудь интересного, но увы - ничего не находишь.
btn kletochka, Назад
end

:razbit
pln Разбить чужую вазу - хорошее дело, но чем? Ломиком для хомяков?
btn vaza02, Назад
end

:vaza02
if steklo=0 then pln Ты стоишь около красивой стеклянной вазы. & btn tolk2, Толкнуть вазу & btn razbit, Разбить вазу
if steklo=1 then pln Ты стоишь около того места где раньше стояла & pln красивая стеклянная ваза. 
if spichka=0 and salasik=0 then btn spichka, Взять из шалашика спичку
if salasik=0 then pln Здесь еще есть шалашик из спичек. & btn salasik1, Разобрать шалашик
if salasik=1 then btn salasik2, Собрать шалашик
btn stolik2002, К клетке
end

:tolk2
pln Ты мучался около часа, прежде чем тебе удалось ее столкнуть.
pln Ты поглядел вниз. Ваза картинно разбилась на миллионы мелких
pln осколков. Теперь пол усыпан стеклом.
steklo=1
btn vaza02, Назад
end

:spichka
pln Ты берешь из шалашика спичку - все равно их там полно !
inv+ Спичка
spichka=1
btn vaza02, Назад
end

:salasik2
if not Жевачка then pln У тебя нет жевачки чтобы закрепить шалашик.
if Жевачка and Коробок then pln Ты снова собираешь шалашик и закрепляешь его жевачкой & inv- Коробок & inv- Жевачка & salasik=0
btn vaza02, Назад
end

:salasik1
salasik=1
pln Ты разбираешь шалашик и снова получаешь коробок и
pln жевачку.
inv+ Коробок
inv+ Жевачка
btn vaza02, Назад
end

:podpol
pln Ты на полу. В углу высится горсть шелухи от семечек,
pln стоит стул, стол с которого болтается прилепленная на
p жвачку веревка,
if steklo=1 and polik=0 then p пол засыпан осколками вазы,
if steklo=1 and polik=1 then p пол засыпан осколками вазы, политых молоком,
if kotik=0 then pln а около двери которая ведет на улицу сидит кот которого & pln ты совсем недавно облил молоком из поилки стоящей в & pln твоей бывшей клетке.
if kotik=1 then pln а около двери которая ведет на улицу кота теперь нет.
if kombikorm=1 then btn pokormka, Подразнить кота кусочком колбасы
if bulbulbul=1 and steklo=1 then btn milkpol, Полить молоком осколки вазы
btn street2222, Подойти к двери на улицу
btn semiana, Покопаться в шелухе
btn verevka, Залезть по веревке на стол.
btn stuul, Подойти к стулу.
end

:razbor
if polik=1 then pln Ты стараясь не подскользнуться на молоке и не наступить на стекла подходишь к двери аккуратно поддеваешь ее снизу лапкой и открываешь ее.
if polik=0 then pln Ты стараясь не подскользнуться на молоке подходишь к двери аккуратно поддеваешь ее снизу лапкой и открываешь ее.
pln Ты открываешь и - УРА !!! - за дверью видишь двор (Правда для тебя это настоящее поле) ! Ты решительно слезаешь с крыльца и делаешь шаг в сторону забора.
goto end
end

:street2222
if kotik=0 then pln Ты не обращая внимания на кота подходишь к двери. Кот подбегает к тебе и съедает тебя. & perkill & invkill & btn play, Сначала
if kotik=1 then goto razbor
end

:pokor1
if polik=0 then pln Кот увидев колбасу в твоей лапке радостно несется к тебе, но внезапно налетает на стекла, и с визгом уносится в другую комнату.
if polik=1 then pln Кот увидев колбасу в твоей лапке радостно несется к тебе, но внезапно подскальзывается на молоке, падает на стекла, и с визгом уносится в другую комнату.
kotik=1
btn podpol, Назад
end

:pokor2
pln Кот подбегает к тебе и съедает колбасу. И тебя заодно.
invkill
perkill
btn play, Сначала
end

:pokormka
if steklo=1 then goto pokor1
if steklo=0 then goto pokor2
end

:comby2
pln Ты аккуратно засовываешь его за щеку.
inv+ Кусок колбасы
kombikorm=1
btn podpol, Назад
end

:comby1
pln Ты радостно разжевываешь его не обращая внимания на странный привкус. Но когда ты на него обратил внимание уже было слишком поздно.
invkill
perkill
btn play, Сначала
end

:semiana
if kombikorm=1 then pln Ты перерыл горстку шелухи но ничего не нашел.
if kombikorm=0 then pln Ты не поверишь, но в шелухе ты нашел довольно крупный кусок колбасы. & btn comby1, Съесть его & btn comby2, Взять его 
btn podpol, Назад
end

:milkpol
ln Ты поливаешь пол (И осколки заодно) остатками молока. Теперь пол вдобавок еще и скользкий.
inv- Остатки молока
polik=1
btn podpol, Назад
end

:fire2222
pln Только ты отошел от стула, как кот который все это время 
p внимательно наблюдал за тобой,
if steklo=1 then pln направился прямиком к тебе (То есть не по стеклам). "Дело дрянь..."- только и успел подумать ты. & pln Перед тем как кот тебя одним махом проглотил.
if steklo=0 then pln направился прямиком к тебе. "Дело дрянь..."- только и успел подумать ты. & pln Перед тем как кот тебя одним махом проглотил.
invkill
perkill
btn play, Сначала
end

:fireattack
pln Ты попробовал чиркнуть спичкой об стул и она тут же зажглась !
pln Ты поднес ее к стулу и стул медленно, но верно разгорелся.
btn fire2222, Отойти
btn firekill, Посмотреть на пламя
if bulbulbul=1 then btn firemilk, Затушить стул молоком
end

:firemilk
pln Ты поливаешь молоком не успевший загореться стул и он затухает. 
inv- Остатки молока
btn fire2222, Отойти
end

:firekill
cls
pln Глядя как зачарованный на пламя ты не заметил как сгорел сам.
invkill
perkill
btn play, Сначала
end

:stuul
pln Ты стоишь около большого деревянного стула.
if spichka=1 then btn fireattack, Поджечь стул спичкой
btn podpol, Назад
end

:spusk
if salasik=0 then pln Ты со свистом съехал со стола по веревке и & pln оказался на полу. & goto podpol
if salasik=1 and steklo=0 then pln Ты начал съезжать и тут вспомнил про шалашик из спичек. & pln Но было уже слишком поздно... & invkill & perkill & btn play, Сначала
if salasik=1 and steklo=1 then pln Ты со свистом съехал со стола по веревке и & pln оказался на полу. & goto podpol
end

:pds02
pln На нижней стороне стола прилеплено много жевачек.
if Жевачка then pln У тебя уже есть одна, и больше тебе не нужно.
if not Жевачка then pln Ты отлепил себе одну на память. & inv+ Жевачка
btn stolik2002, Назад
end

:end
pln
pln Поздравляю ! Вы прошли третью серию приключений хомяка Семена ! Ждите следующую серию: Похождения отважного хомяка Семена-4: Жизнь Жуков !
pln Пишите человеку который придумал Семена на triangle@zmail.ru
pln Пишите мне на ghost2002@xakep.ru
pln
pln kulhacker.boom.ru 
pln bunker-2k.narod.ru
btn 1, В меню
end